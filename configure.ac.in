dnl
dnl Copyright (c) 2006-2012
dnl         The Xfce development team. All rights reserved.
dnl
dnl Originally written for Xfce by Benedikt Meurer <benny@xfce.org>
dnl Modified by Stephan Arts <stephan@xfce.org>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([ristretto_version_major], [0])
m4_define([ristretto_version_minor], [10])
m4_define([ristretto_version_micro], [0])
m4_define([ristretto_version_build], [r@REVISION@])
m4_define([ristretto_version_tag], [git]) # Leave empty for releases
m4_define([ristretto_version], [ristretto_version_major().ristretto_version_minor().ristretto_version_micro()ifelse(ristretto_version_tag(), [], [], [ristretto_version_tag()-ristretto_version_build()])])

dnl *******************************************
dnl *** Debugging support for SVN snapshots ***
dnl *******************************************
m4_define([ristretto_debug_default], [ifelse(ristretto_version_tag(), [git], [full], [minimum])])

AC_COPYRIGHT([Copyright (c) 2006-2012
        The Xfce development team. All rights reserved.

Written for Xfce by Stephan Arts <stephan@xfce.org>.])

AC_INIT([ristretto], [ristretto_version], [])
AC_PREREQ([2.50])

RISTRETTO_VERSION=ristretto_version
AM_INIT_AUTOMAKE([1.8 no-dist-gzip dist-bzip2 tar-ustar foreign])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX
AM_CONDITIONAL([HAVE_CYGWIN], [test "`uname | grep \"CYGWIN\"`" != ""])


dnl check for standard header files
AC_PROG_CC
IT_PROG_INTLTOOL([0.35], [no-xml])


AC_HEADER_STDC

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

XDT_CHECK_PACKAGE([LIBEXIF], [libexif], [0.6.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.22.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.42.0])
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.42.0])
XDT_CHECK_PACKAGE([GOBJECT], [gobject-2.0], [2.42.0])
XDT_CHECK_PACKAGE([GIO], [gio-2.0], [2.42.0])
XDT_CHECK_PACKAGE([GIO_UNIX], [gio-unix-2.0], [2.42.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.10.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.10.0])

XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [4.12.1])

XDT_CHECK_PACKAGE([CAIRO], [cairo], [1.8.0])

XDT_CHECK_LIBX11()

AC_CHECK_HEADERS([magic.h],, [libmagic=false])
AC_CHECK_LIB(magic, [magic_open], [MAGIC_LIBS="-lmagic"],[libmagic=false])
AC_SUBST(MAGIC_LIBS)

AC_CHECK_LIBM
AC_SUBST(LIBM)

dnl check for debugging support
XDT_FEATURE_DEBUG

AC_OUTPUT([
Makefile
po/Makefile.in
src/Makefile
icons/Makefile
icons/16x16/Makefile
icons/24x24/Makefile
icons/32x32/Makefile
icons/48x48/Makefile
icons/128x128/Makefile
icons/scalable/Makefile
])

echo "----------------------------------------"
echo "Build Configuration for $PACKAGE $VERSION:"
if test "$libmagic" = "false"; then
echo "  libmagic support: no"
else
echo "  libmagic support: yes"
fi
echo ""
echo "The binary will be installed in $prefix/bin"
echo "----------------------------------------"
